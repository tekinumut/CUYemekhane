apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'

def keystorePropertiesFile = rootProject.file("Certificates/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
// Secret Keys
def secretsFile = rootProject.file("Certificates/secrets.properties")
def secretProperties = new Properties()
secretProperties.load(new FileInputStream(secretsFile))

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
            storeFile file('../Certificates/CuYemekhane.jks')
        }
    }

    defaultConfig {
        applicationId "com.tekinumut.cuyemekhane"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 103
        versionName "1.03"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            resValue "string", "app_name", "CuTestYemek"
            resValue "string", "admob_app_id", "${secretProperties['admobAppIdTest']}"
            resValue "string", "adUnitIdMainBanner", "${secretProperties['adUnitIdBannerAdTest']}"
            resValue "string", "adUnitIdRemoveBanner", "${secretProperties['adUnitIdRewardedAdTest']}"
            resValue "string", "adUnitIdMonthlyList", "${secretProperties['adUnitIdRewardedAdTest']}"
            applicationIdSuffix '.debug'
        }
        release {
            minifyEnabled true
            resValue "string", "app_name", "Çukurova Üniversitesi Yemekhane"
            resValue "string", "admob_app_id", "${secretProperties['admobAppId']}"
            resValue "string", "adUnitIdMainBanner", "${secretProperties['adUnitIdMainBanner']}"
            resValue "string", "adUnitIdRemoveBanner", "${secretProperties['adUnitIdRemoveBanner']}"
            resValue "string", "adUnitIdMonthlyList", "${secretProperties['adUnitIdMonthlyList']}"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${variant.applicationId}-${variant.versionName}.apk"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    buildFeatures {
        dataBinding  true
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "com.google.android.material:material:1.4.0"
    implementation "androidx.browser:browser:1.3.0"

    // Firebase
    implementation platform("com.google.firebase:firebase-bom:28.4.1")
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-perf-ktx'

    // Ads
    implementation "com.google.android.gms:play-services-ads:20.4.0"

    //3rd PARTY LIBRARIES BEGIN

    // DatePickerDialog
    implementation 'com.wdullaer:materialdatetimepicker:4.2.3'

    // Retrofit dependencies
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofit_version"

    // OkHttp
    implementation(platform("com.squareup.okhttp3:okhttp-bom:4.9.2"))
    implementation("com.squareup.okhttp3:logging-interceptor")

    // JSOUP - HTML PARSER
    implementation 'org.jsoup:jsoup:1.14.3'

    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'
    //3rdPARTY LIBRARIES END


    // Navigation
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'

    // ROOM dependencies
    def room_version = "2.3.0"
    implementation "androidx.room:room-runtime:$room_version"
    // optional - Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"
    // For Kotlin use kapt instead of annotationProcessor
    kapt "androidx.room:room-compiler:$room_version"

    // Lifecycle and Viewmodel dependencies
    def lifecycle_version = "2.3.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    // Coroutines dependencies
    def coroutines_version = "1.5.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
